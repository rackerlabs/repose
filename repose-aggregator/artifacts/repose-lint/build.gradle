apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'nebula.ospackage'

configurations {
    fatJar
}

dependencies {
    compile "org.scala-lang:scala-library"
    compile "org.scala-lang.modules:scala-xml_2.11"
    compile "com.typesafe:config"
    compile "com.typesafe.play:play-json_$scalaMajDotMin"
    compile "com.github.scopt:scopt_$scalaMajDotMin"

    testCompile "junit:junit"
    testCompile "org.scalatest:scalatest_$scalaMajDotMin"
    testCompile "org.mockito:mockito-core"
}

jar {
    manifest {
        attributes 'Main-Class': 'org.openrepose.lint.Lint'
    }
}

shadowJar {
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
}

artifacts {
    fatJar shadowJar
}

build.dependsOn shadowJar

signing {
    required { gradle.taskGraph.hasTask("publish") }
    sign shadowJar
}

publishing {
    publications {
        nebula(MavenPublication) {
            artifact shadowJar
            if(project.hasProperty('release')) {
                artifact source: signShadowJar.singleSignature, extension: 'asc'
                artifact source: buildDeb, extension: 'deb'
                artifact source: buildRpm, extension: 'rpm'
            }
        }
    }
}

ospackage {
    packageName = "repose-lint"
    os = LINUX
    packageDescription 'A command-line utility which flags suspicious usage of Repose'
    priority 'optional'
    packageGroup 'Applications/Internet'
    maintainer 'Repose Development <ReposeCore@Rackspace.com>'
    url 'http://openrepose.org'

    if(project.hasProperty('signing.keyId') && project.hasProperty('signing.password') && project.hasProperty('signing.secretKeyRingFile')) {
        signingKeyId project.getProperty('signing.keyId')
        signingKeyPassphrase project.getProperty('signing.password')
        signingKeyRingFile file(project.getProperty('signing.secretKeyRingFile'))
    }

    into '/usr/share/repose'

    //the jar
    from(shadowJar.outputs.files) {
        rename('(.*).jar', 'repose-lint.jar')
        user 'root'
        permissionGroup 'root'
        fileMode 0555
    }

    //the init script
    from('src/config/scripts') {
        user 'root'
        permissionGroup 'root'
        fileMode 0755
    }
    link('/usr/bin/repose-lint', '/usr/share/repose/repose-lint')

    //the license info
    from(["$rootDir/LICENSE.txt", "$rootDir/CONTRIBUTORS.txt", 'src/config/docs/DEPENDENCIES.txt']) {
        into '/usr/share/doc/repose-lint'
        user 'root'
        permissionGroup 'root'
        fileMode 0444
    }
}

buildDeb {
    requires('java8-runtime-headless')
}

buildRpm {
    release = 1

    requires('java', '1.8.0', GREATER | EQUAL)
}
