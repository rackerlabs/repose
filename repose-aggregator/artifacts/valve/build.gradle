apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'nebula.ospackage'

configurations {
    fatJar
}

dependencies {
    compile project(":repose-aggregator:core:repose-core-api")
    compile project(":repose-aggregator:commons:commons-configuration")
    compile project(":repose-aggregator:core:repose-core")
    compile "org.scala-lang:scala-library"
    compile "com.typesafe.scala-logging:scala-logging-slf4j_$scalaMajDotMin"
    compile "org.slf4j:jcl-over-slf4j"
    compile "com.github.scopt:scopt_$scalaMajDotMin"
    compile "com.typesafe:config"
    compile "org.springframework:spring-core"
    compile "org.springframework:spring-context"
    compile "org.springframework:spring-web"
    compile "org.springframework:spring-beans"
    compile "org.eclipse.jetty:jetty-servlet"
    compile "org.eclipse.jetty:jetty-server"
    compile "org.eclipse.jetty:jetty-util"

    testCompile "junit:junit"
    testCompile "org.scalatest:scalatest_$scalaMajDotMin"
    testCompile "org.apache.httpcomponents:httpclient"
}

processResources {
    expand(reposeVersion: version, jettyVersion: '9.2.0.v20140526') //todo: try to reuse this from the version properties
}

jar {
    manifest {
        attributes 'Main-Class': 'org.openrepose.valve.Main'
    }
}

shadowJar {
    archiveName = 'repose-valve.jar'
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
    exclude 'META-INF/org/apache/logging/log4j/core/config/plugins/Log4j2Plugins.dat' //todo: this might bone us
}

artifacts {
    fatJar shadowJar
}

build.dependsOn shadowJar

signing {
    required { gradle.taskGraph.hasTask("publish") }
    sign shadowJar
}

publishing {
    publications {
        nebula(MavenPublication) {
            artifact shadowJar {
                //this is neccessary because i changed the archive name in shadow jar,
                // so i wouldn't have to go hack the crap out of the testing framework
                groupId project.group
                artifactId project.name
                version project.version
                classifier 'all'
            }
            if(project.hasProperty('release')) {
                artifact source: signShadowJar.singleSignature, extension: 'asc'
                artifact source: buildDeb, extension: 'deb'
                artifact source: buildRpm, extension: 'rpm'
            }
        }
    }
}

ospackage {
    packageName = "repose-valve"
    release = 1
    os = LINUX
    packageDescription 'Package for the Repose Valve Deployment'
    priority 'optional'
    packageGroup 'Applications/Internet'
    maintainer 'Repose Development <repose-development@lists.openrepose.org>'
    url 'http://openrepose.org'

    //todo: write and submit a pr to the plugin that does this automatically
    if(project.hasProperty('signing.keyId') && project.hasProperty('signing.password') && project.hasProperty('signing.secretKeyRingFile')) {
        signingKeyId project.getProperty('signing.keyId')
        signingKeyPassphrase project.getProperty('signing.password')
        signingKeyRingFile file(project.getProperty('signing.secretKeyRingFile'))
    }

    into '/usr/share/repose'

    //todo: maybe enhance the plugin so that directory commands can take owners, not just file modes
    //the main directory
    from('../src/config/DELETE-ME') {
        createDirectoryEntry true
        user 'root'
        permissionGroup 'root'
        fileMode 555
    }

    //the web app
    from(shadowJar.outputs.files) {
        user 'root'
        permissionGroup 'root'
        fileMode 555
    }

    //the filter bundle directory
    from('../src/config/DELETE-ME') {
        into '/usr/share/repose/filters'
        createDirectoryEntry true
        user 'repose'
        permissionGroup 'repose'
        fileMode 755
    }

    //the artifact exploding directory
    from('../src/config/DELETE-ME') {
        into '/var/repose'
        createDirectoryEntry true
        user 'repose'
        permissionGroup 'repose'
        fileMode 750
    }

    //the log directory
    from('../src/config/DELETE-ME') {
        into '/var/log/repose'
        createDirectoryEntry true
        user 'repose'
        permissionGroup 'repose'
        fileMode 750
    }

    //the log rotate settings
    from('../src/config/logrotate.d') {
        into '/etc/logrotate.d'
        createDirectoryEntry false
        user 'repose'
        permissionGroup 'repose'
        fileMode 644
    }

    //the configs directory
    from('../src/config/DELETE-ME') {
        into '/etc/repose'
        createDirectoryEntry true
        user 'repose'
        permissionGroup 'repose'
        fileMode 770
    }

    //the basic configs
    from('../src/config/filters') { //not really filter config, but it's where the bundles are putting them for now, so i'm maintaining consistency
        into '/etc/repose'
        createDirectoryEntry true
        user 'repose'
        permissionGroup 'repose'
        fileMode 640
        fileType CONFIG | NOREPLACE
    }

    //the systemd service
    from('../src/config/systemd/repose-valve.service') {
        into '/lib/systemd/system'
        createDirectoryEntry false
        user 'root'
        permissionGroup 'root'
        fileMode 644
    }

    //the systemd local overrides
    from('../src/config/systemd/local.conf') {
        into '/etc/systemd/system/repose-valve.service.d'
        createDirectoryEntry true
        user 'root'
        permissionGroup 'root'
        fileMode 644
    }

    //the systemd wrapper script
    from('../src/config/scripts/repose-valve-systemd') {
        rename('repose-valve-systemd', 'repose-valve')
        into '/usr/share/repose'
        createDirectoryEntry false
        user 'root'
        permissionGroup 'root'
        fileMode 755
    }

    //todo: remove the old once we are pure gradle
    //the sysconfig
    from('../src/config/sysconfig') {
        into '/etc/sysconfig'
        createDirectoryEntry false
        user 'root'
        permissionGroup 'root'
        fileMode 644
    }

    //clean up script
    from('../src/config/scripts/clean-repose-deploy') {
        user 'root'
        permissionGroup 'root'
        fileMode 755
    }
    link('/usr/bin/clean-repose-deploy', '/usr/share/repose/clean-repose-deploy')

    //the license info
    from(["$rootDir/LICENSE.txt", "$rootDir/CONTRIBUTORS.txt", '../src/config/docs/DEPENDENCIES.txt']) {
        into '/usr/share/doc/repose-war'
        user 'root'
        permissionGroup 'root'
        fileMode 444
    }

    //todo: consider submitting enhancements to this plugin that allow version numbers
    provides('repose')
    conflicts('repose-war')
    recommends('repose-filter-bundle')
    suggests('repose-extensions-bundle')
    suggests('logrotate')
}

buildDeb {
    //the init.d
    from('../src/config/scripts/repose-valve-deb') {
        rename('repose-valve-deb', 'repose-valve')
        into '/etc/init.d'
        createDirectoryEntry false
        user 'root'
        permissionGroup 'root'
        fileMode 755
    }

    requires('java8-runtime-headless')
    preInstall file('../src/config/scripts/preinst-deb')
    //todo: enhance the plugin so that filetype is properly respected
    configurationFile '/etc/repose/container.cfg.xml'
    configurationFile '/etc/repose/system-model.cfg.xml'
    configurationFile '/etc/repose/response-messaging.cfg.xml'
    configurationFile '/etc/repose/log4j2.xml'
    configurationFile '/etc/init.d/repose-valve'
}

buildRpm {
    //the init.d
    from('../src/config/scripts/repose-valve-rpm') {
        rename('repose-valve-rpm', 'repose-valve')
        into '/etc/init.d'
        createDirectoryEntry false
        user 'root'
        permissionGroup 'root'
        fileMode 755
    }

    requires('java', '1.8.0', GREATER | EQUAL)
    requires('daemonize')
    preInstall file('../src/config/scripts/preinst-rpm')
    postInstall('/sbin/chkconfig --add repose-valve')
}
